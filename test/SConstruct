# -*- coding: utf-8; mode: Python -*-
# SConstruct for ra/test

# (c) Daniel Llorens - 2015-2016, 2018
# This library is free software; you can redistribute it and/or modify it under
# the terms of the GNU Lesser General Public License as published by the Free
# Software Foundation; either version 3 of the License, or (at your option) any
# later version.

# FIXME Shared pieces with {examples,bench,docs}/SConstruct

import os, string, atexit
from os.path import join, abspath
import imp
ra = imp.load_source('ra', '../config/ra.py')

# FIXME pick BLAS flags from here.
try:
    Import('top')
except:
    top = {}

vars = Variables()
vars.AddVariables(PathVariable('variant_dir', 'build directory', '.', PathVariable.PathIsDirCreate))

env = Environment(options=vars,
                  ENV=dict([(k, os.environ[k] if k in os.environ else '')
                            for k in ['PATH', 'HOME', 'TERM', 'LD_RUN_PATH', 'DYLD_LIBRARY_PATH',
                                      'RPATH', 'LIBRARY_PATH', 'TEXINPUTS', 'GCC_COLORS', 'BOOST_ROOT',
                                      'RA_USE_BLAS']]))
variant_dir = env['variant_dir']

for var, default in [('CC', 'gcc'), ('CXX', 'g++'), ('FORTRAN', 'gfortran')]:
    ra.take_from_environ(env, var, default=default)
for var in ['FORTRANFLAGS', 'LINKFLAGS', 'CCFLAGS', 'CXXFLAGS']:
    ra.take_from_environ(env, var, wrapper=string.split)
for var in ['RPATH', 'LIBPATH']:
    ra.take_from_environ(env, var, wrapper=lambda x: [x])

arch = os.popen('../config/config.guess').read()

if arch.find('apple-darwin') >= 0:
    archflags=['-march=native', '-Wa,-q']
    ld_blas = {'LINKFLAGS': ' -framework Accelerate ', 'LIBS': []}
else:
    archflags=['-march=native']
    ld_blas = {'LINKFLAGS': '', 'LIBS': ['blas']}

env.Prepend(CPPPATH=['..', '.'],
            CCFLAGS=archflags if str(env['CCFLAGS']).strip()=='' else '',
            CXXFLAGS=['-std=c++17', '-Wall', '-Werror', '-fdiagnostics-color=always', '-Wno-unknown-pragmas',
                      '-finput-charset=UTF-8', '-fextended-identifiers',
                      '-Wno-error=strict-overflow', '-Werror=zero-as-null-pointer-constant',
                      #'-Wconversion',
                      # '-funsafe-math-optimizations', # TODO Test with this.
                  ])

env_blas = env.Clone()
if 'RA_USE_BLAS' in env['ENV'] and env['ENV']['RA_USE_BLAS']=='1':
    print("[%s] BLAS will be used." % Dir('.').path)
    env_blas.Append(CPPDEFINES={'RA_USE_BLAS': 1})
    env_blas.Append(LINKFLAGS=ld_blas['LINKFLAGS'])
    env_blas.Append(LIBS=ld_blas['LIBS'])
else:
    print("[%s] BLAS won't be used." % Dir('.').path)

tester = ra.to_test_ra(env, variant_dir)

[tester(test)
 for test in ['old', 'ra-0', 'ra-1', 'ra-2', 'ra-3', 'ra-4', 'ra-5', 'ra-6', 'ra-7', 'ra-8', 'ra-9',
              'ra-11', 'types', 'io', 'from', 'nested-0', 'owned', 'ownership', 'ply',
              'small-0', 'small-1', 'big-0', 'big-1',
              'return-expr', 'reduction', 'frame-old', 'frame-new', 'compatibility',
              'dual', 'ra-dual', 'mem-fn', 'stl-compat', 'where', 'tuplelist', 'wedge',
              'operators', 'tensorindex', 'explode-0', 'wrank', 'optimize', 'reshape', 'concrete',
              'bench', 'iterator-small', 'const', 'tensor-indices', 'list9', 'early', 'macros',
              # 'end'
              ]]

tester('ra-10', target='ra-10a', cxxflags=['-O3'], cppdefines={'RA_CHECK_BOUNDS': '0'})
tester('ra-10', target='ra-10b', cxxflags=['-O1'], cppdefines={'RA_CHECK_BOUNDS': '0'})
tester('ra-10', target='ra-10c', cxxflags=['-O3'], cppdefines={'RA_CHECK_BOUNDS': '1'})
tester('ra-10', target='ra-10d', cxxflags=['-O1'], cppdefines={'RA_CHECK_BOUNDS': '1'})

if 'skip_summary' not in top:
    atexit.register(lambda: ra.print_summary(GetBuildFailures, 'ra/test'))
